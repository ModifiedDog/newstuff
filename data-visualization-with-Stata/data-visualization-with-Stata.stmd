% Data Visualization With Stata
% Andy Grogan-Kaylor
% `s c(current_date)`

---
geometry: margin=1 in
---

# Introduction 

<style>h1 {color: #00274C;} h2 {color: #2F65A7;} blockquote {border-left: 5px solid #ffcb05; margin: 1.5em 10px; padding: 0.5em 10px;}</style>

* Stata is a powerful and intuitive data analysis program. 
* Learning how to graph in Stata is an important part of learning how to use Stata. Yet, the default graphs in Stata can sometimes be less than optimal. 
* This document is an introduction to (a) basic graphing ideas in Stata; and (b) a quick note on the use of schemes to make your Stata graphs look more professional.

# What are Variables?

* By variables, I simply mean the columns of data that you have.
* For our purposes, you may think of variables as synonymous with questionnaire items, or columns of data.

# Variable Types

* *categorical variables* represent unordered categories like *race*, *ethnicity*, *neighborhood*, *religious affiliation*, or *place of residence*.
* *continuous variables* represent a continuous scale like *income*, a *mental health scale*, or a *measure of life expectancy*.

# A Data Visualization Strategy

Once we have discerned the type of variable that have, there are two followup questions we may ask before deciding upon a graphing strategy:

* Is our graph about **one thing at a time**?
  + How much of *x* is there? 
  + What is the distribution of *x*?
* Is our graph about **two things at a time**?
  + What is the relationship of *x* and *y*?
  + How are *x* and *y* associated?


# Data Source

![Norway Spruce and Larch Forest in Austrian Alps](fotolia-small.jpg){ width=40% }

Image Source: [https://ec.europa.eu/jrc/en/research-topic/forestry/qr-tree-project/norway-spruce](https://ec.europa.eu/jrc/en/research-topic/forestry/qr-tree-project/norway-spruce)

The data used in this example are derived from the R package *Functions and Datasets for "Forest Analytics with R"*.

According to the documentation, the source of these data are: "von Guttenberg’s Norway spruce (Picea abies [L.] Karst) tree measurement data."

![Old Tjikko, a 9,550 Year Old Norway Spruce in Sweden](Old-Tjikko-Small.jpg){ width=20% }

The documentation goes on to further note that:

> "The data are measures from 107 trees. The trees were selected as being of average size from healthy and well stocked stands in the Alps."

    use gutten.dta, clear
		
# Variables

`site` Growth *quality* class of the tree’s habitat. 5 levels.

`location` Distinguishes tree *location*. 7 levels.

`tree` An identifier for the tree within location.

`age_base` The tree age taken at ground level.

> For some purposes, it might be best to use a centered age variable, centered at the grand mean of tree age: 

    egen ageMEAN = mean(age_base)
	
	generate ageCENTERED = age_base - ageMEAN
	
`height` Tree height, m.

`dbh_cm` Tree diameter, cm.

`volume` Tree volume.

`age_bh` Tree age taken at 1.3 m.

`tree.ID` A factor uniquely identifying the tree.

# Graphs

## One Continuous Thing At A Time

    histogram height, title("Tree Height")
	
    graph export myhistogram.png, width(500) replace

![Histogram Of Tree Height](myhistogram.png)

## One Categorical Thing At A Time

    graph bar, over(location) title("Tree Location")
	
	graph export mybargraph.png, width(500) replace

![Bar Graph Of Tree Location](mybargraph.png)

## Continuous by Continuous

    twoway scatter height age_base, title("Tree Height by Age")
	
	graph export myscatter.png, width(500) replace

![Scatterplot Of Tree Height By Age](myscatter.png)

## Categorical by Categorical

    graph bar, over(site) over(location) title("Tree Site Growth Quality by Location")
	
	graph export mybargraph2.png, width(500) replace

![Bar Graph Of Tree Site By Location](mybargraph2.png)

## Continuous by Categorical

    graph bar height, over(location) title("Tree Height by Location")
	
	graph export mybargraph3.png, width(500) replace

![Bar Graph Of Mean Tree Height By Location](mybargraph3.png)

# Schemes

Stata *graph schemes* can substantially improve the look of a graph. Built in graph schemes include `sj` and `economist`. 

`lean2` (type `findit lean2` in the Stata Command Window) is a user written scheme that is very helpful when preparing graphics for publication. I have written a [Stata Michigan graph scheme](https://agrogan1.github.io/Stata/) that can be installed.

## Continuous by Continuous

    twoway scatter height age_base, title("Tree Height by Age") scheme(michigan)
	
	graph export myscatterM.png, width(500) replace

![Scatterplot Of Tree Height By Age With Michigan Graph Scheme](myscatterM.png)

    twoway scatter height age_base, title("Tree Height by Age") scheme(lean2)

	graph export myscatterL.png, width(500) replace

![Scatterplot Of Tree Height By Age With lean2 Graph Scheme](myscatterL.png)

## Continuous by Categorical 

> Note that in the graph below, I have used the `asyvars` option to give different colors to the different bars.

	graph bar height, over(location) asyvars title("Tree Height by Location") scheme(michigan)
	
	graph export mybarM.png, width(500) replace

![Bar Graph Of Mean Tree Height By Location With Michigan Graph Scheme](mybarM.png)

	graph bar height, over(location) asyvars title("Tree Height by Location") scheme(lean2)
	
	graph export mybarL.png, width(500) replace

![Bar Graph Of Mean Tree Height By Location With lean2 Graph Scheme](mybarL.png)






