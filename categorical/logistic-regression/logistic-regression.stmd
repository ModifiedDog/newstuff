% Logistic Regression
% Andy Grogan-Kaylor
% `s c(current_date)`

---
geometry: margin=1 in
---

# Key Concepts and Commands

<style>h1 {color: #00274C;} h2 {color: #2F65A7}</style>

* Fitting a Curve to 2 Possible Values

![](logistic.png){width=25%}

* Linear models, probit and logit

* `y x1 x2 ...` $\leftarrow$ $\rightarrow$ $F(y) = \beta_0 + \beta x_1 + \beta x_2 ...$

* `regress y x1 x2` OLS; Linear Model

* `logit y x1 x2` Logistic Regression

* `probit y x1 x2` Probit Regression

* `glm ...`

# Limited Dependent Variables

* Categorical Dependent Variable
* Binary Dependent Variable
* Limited Dependent Variable

# General Social Survey

    use "/Users/agrogan/Box Sync/DATA WAREHOUSE/General Social Survey Panel Data/GSS_panel2010w123_R6 - stata.dta", clear 
	
    codebook polviews_3 // what does this variable look like?
	
# Data Management

    recode happy_3 (1/2 = 1)(3=0), generate(happy_3_D) // dichotomize

	tabulate happy_3 happy_3_D // double check
	
	recode polviews_3 (1/3 = 1)(4/7 = 0), generate(liberal_3_D) // dichotomize
	
	generate coninc_3_10K = coninc_3 / 10000 // income in $10K chunks
	
	label variable coninc_3_10K "Income 10K Chunks"
		
	egen income_group_3 = cut(coninc_3), group(3) // divide income into three groups
	
	keep polviews_3 liberal_3_D ///
	race_3 ///
	happy_3 happy_3_D ///
	coninc_3 coninc_3_10K  income_group_3 // keep only some variables
	
	save GSSsmall.dta, replace
	
# Visualize	

	twoway (scatter liberal_3_D coninc_3, jitter(5) legend(off)) ///
	(lowess liberal_3_D coninc_3), scheme(burd) ///
	title("Liberal Attitudes by Income") 
	
    graph export liberal-income.png, width(500) replace
	
![Liberal Attitudes and Income](liberal-income.png)

# Linear Probability Model

    regress liberal_3_D i.race_3 i.income_group_3
	
# Normal and Cumulative Normal Distribution

    clear all
	
	set obs 100 // 100 observations
	
	generate z = runiform(-5, 5) // randomly distributed z scores

    generate mynormaldensities = normalden(z) // normal densities
	
    generate myprobabilities = normal(z) // cumulative normal probabilities
	
	twoway scatter mynormaldensities myprobabilities z, scheme(michigan)
	
	graph export normal.png, width(500) replace
	
![Standard and Cumulative Normal Curves](normal.png)

# The Probit Model

    use GSSsmall.dta, clear

    probit liberal_3_D i.race_3 i.income_group_3
	
# The Logistic Distribution

    clear all
	
	set obs 100 // 100 observations
	
	generate z = runiform(-5, 5) // randomly distributed z scores

    generate mylogisticdensities = logisticden(z) // logistic densities
	
	generate mylogisticprobabilities = logistic(z) // cumulative logistic probabilities
	
	twoway scatter mylogisticdensities mylogisticprobabilities z, scheme(michigan)
	
	graph export logistic.png, width(500) replace
	
![Standard and Cumulative Logistic Curves](logistic.png)

# The Logit (Logistic) Model

    use GSSsmall.dta, clear

    logit liberal_3_D i.race_3 i.income_group_3

# Comparison of LPM, Probit and Logistic Coefficients

NB: Negative vs. positive $\beta$.

    quietly probit liberal_3_D i.race_3 i.income_group_3
	
	est store myprobit

    quietly logit liberal_3_D i.race_3 i.income_group_3
	
	est store mylogit
	
	est table myprobit mylogit

# Logistic Model (2)

> Derivation of logistic model from linear probability model. Using instructor notes

$$
    \ln \left( \frac{P(y)}{1-P(y)} \right) = \beta_0 + \beta_1 x_1 + \beta_2 x_2 + ...
$$
	
# Interpretation of Odds Ratios (Robert Mare)

$$
    0 < OR < 1	
$$ 

> indicates that an increase in x is associated with a decrease in y.

$$ 
    1 < OR < \infty 
$$

> indicates that an increase in x is associated with an increase in y.

# Logistic Model With Odds Ratios

    logit liberal_3_D i.race_3 i.income_group_3, or

# A Poem About Logistic Regression

# Complete Determination

See handout

# Rare Events

* Statistical power
* Complete determination

# Predicted Probabilities

Discussion

# The General Linear Model

# Interaction Terms

See interactive demo, or example script.

[https://agrogan1.github.io/newstuff/categorical/logistic-interactions-2/logistic-interactions-2.html](https://agrogan1.github.io/newstuff/categorical/logistic-interactions-2/logistic-interactions-2.html)

