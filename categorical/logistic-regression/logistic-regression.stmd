% Logistic Regression
% Andy Grogan-Kaylor
% `s c(current_date)`

---
geometry: margin=1 in
---

# Key Concepts and Commands

<style>h1 {color: #00274C;} h2 {color: #2F65A7}</style>

* Fitting a Curve to 2 Possible Values

![](logistic.png){width=30%}

* Linear models, probit and logit

* `y x1 x2 ...` $\leftarrow$ $\rightarrow$ $F(y) = \beta_0 + \beta x_1 + \beta x_2 ...$

* `regress y x1 x2` OLS; Linear Model

* `logit y x1 x2` Logistic Regression

* `probit y x1 x2` Probit Regression

* `glm ...`

# Limited Dependent Variables

* Categorical Dependent Variable
* Binary Dependent Variable
* Limited Dependent Variable

# General Social Survey

    clear all

    set maxvar 10000
	
    use "/Users/agrogan/Box Sync/DATA WAREHOUSE/General Social Survey/GSS7218_R1.DTA", clear
	
    * keep if year == 2018 // keep only most recent year
	
    codebook polviews // what does this variable look like?
	
# Data Management
	
	recode polviews (1/3 = 1)(4/7 = 0), generate(liberal) // dichotomize
	
	generate coninc_10K = coninc / 10000 // income in $10K chunks
	
	label variable coninc_10K "Income 10K Chunks"
		
	egen income_group = cut(coninc), group(3) // divide income into three groups
		
> Reference group for income group is 0
	
	drop if class == 5

	recode hispanic (1 = 1)(else = 0), generate(latinx) // Latinx
	
	keep year polviews liberal ///
	race latinx class ///
	coninc coninc_10K income_group // keep only some variables
	
	save GSSsmall.dta, replace
	
# Visualize	

	twoway qfit liberal coninc, lwidth(thick) scheme(burd) ///
	title("Liberal Attitudes by Income") 
	
    graph export liberal-income.png, width(500) replace
	
![Liberal Attitudes and Income](liberal-income.png)

# Linear Probability Model

    regress liberal i.race i.income_group
	
# Normal and Cumulative Normal Distribution

    clear all
	
	set obs 100 // 100 observations
	
	generate z = runiform(-5, 5) // randomly distributed z scores

    generate mynormaldensities = normalden(z) // normal densities
	
    generate myprobabilities = normal(z) // cumulative normal probabilities
	
	twoway scatter mynormaldensities myprobabilities z, scheme(michigan)
	
	graph export normal.png, width(500) replace
	
![Standard and Cumulative Normal Curves](normal.png)

# The Probit Model

    use GSSsmall.dta, clear

    probit liberal i.race i.latinx i.class i.income_group
	
# The Logistic Distribution

    clear all
	
	set obs 100 // 100 observations
	
	generate z = runiform(-5, 5) // randomly distributed z scores

    generate mylogisticdensities = logisticden(z) // logistic densities
	
	generate mylogisticprobabilities = logistic(z) // cumulative logistic probabilities
	
	twoway scatter mylogisticdensities mylogisticprobabilities z, scheme(michigan)
	
	graph export logistic.png, width(500) replace
	
![Standard and Cumulative Logistic Curves](logistic.png)

# The Logit (Logistic) Model

    use GSSsmall.dta, clear

    logit liberal i.race i.latinx i.class i.income_group

# Comparison of LPM, Probit and Logistic Coefficients

> NB: Negative vs. positive $\beta$. Statistically significant vs. not statistically significant.

    quietly probit liberal i.race i.latinx i.class i.income_group
	
	est store myprobit

    quietly logit liberal i.race i.latinx i.class i.income_group
	
	est store mylogit
	
	est table myprobit mylogit, star

# Logistic Model (2)

> Derivation of logistic model from linear probability model. Using instructor notes

$$
    \ln \left( \frac{P(y)}{1-P(y)} \right) = \beta_0 + \beta_1 x_1 + \beta_2 x_2 + ...
$$
	
# Interpretation of Odds Ratios (Robert Mare)

$$
    0 < OR < 1	
$$ 

> indicates that an increase in x is associated with a decrease in y.

$$ 
    1 < OR < \infty 
$$

> indicates that an increase in x is associated with an increase in y.

# Logistic Model With Odds Ratios

    logit liberal i.race i.latinx i.class i.income_group, or

# A Poem About Logistic Regression

# Complete Determination

See handout

# Rare Events

* Statistical power
* Complete determination

# Predicted Probabilities

> Discussion

# The General Linear Model

# Interaction Terms

See interactive demo, or example script.

[https://agrogan1.github.io/newstuff/categorical/logistic-interactions-2/logistic-interactions-2.html](https://agrogan1.github.io/newstuff/categorical/logistic-interactions-2/logistic-interactions-2.html)

