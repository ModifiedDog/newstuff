% Contingency Tables
% Andy Grogan-Kaylor
% `s c(current_date)` `s c(current_time)`
	
# Key Concepts and Commands

<style>h1 {color: #00274C;} h2 {color: #2F65A7}</style>

# Flipping Two Coins

## Setup

    clear all
	
	set seed 3846
	
Good value labels are **key** here.
	
	label define nickel ///
	1 "heads for nickel" /// 
	0 "tails for nickel" // define value label
	
	label define quarter /// 
	1 "heads for quarter" /// 
	0 "tails for quarter" // define value label

    set obs 1000 // 1000 observations
	
	* curiously it takes around 1000 obs for the proportions
	* below to "take hold"
	
	generate nickel = rbinomial(1, .75) // unfair nickel
	
	generate quarter = rbinomial(1, .5) // fair quarter
	
	label values nickel nickel // assign value label
	
	label values quarter quarter // assign value label

## Crosstabulation
	
	tabulate nickel quarter, row col
	
## Graphing (Mosaic Plot)

    * ssc install spineplot // mosaicplots (spineplots)
	
	* ssc install scheme-burd, replace // BuRd graph scheme
		
	spineplot nickel quarter, scheme(burd)
	
	graph export nickel-quarter.png, width(500) replace
	
![Mosaic Plot](nickel-quarter.png)

## Bar Chart

Does a bar chart work to visualize these relationships?

    graph bar, over(quarter) over(nickel) scheme(burd)
	
	graph export nickel-quarter-bar1.png, width(500) replace
	
![Bar Chart 1](nickel-quarter-bar1.png)
	
Option `asyvars` adds a crucial color element.

    graph bar, over(quarter) over(nickel) scheme(burd) asyvars
	
	graph export nickel-quarter-bar2.png, width(500) replace
	
![Bar Chart 2](nickel-quarter-bar2.png)

And `hbar` may improve legibility even more.

    graph hbar, over(quarter) over(nickel) scheme(burd) asyvars
	
	graph export nickel-quarter-bar3.png, width(500) replace

![Bar Chart 3](nickel-quarter-bar3.png)

# 1961 French Skiiers

    clear all

## Define Matrix

    matrix input FrenchSkiiers = (31, 109 \ 17, 122)

	matrix rownames FrenchSkiiers = Placebo AscorbicAcid
	
	matrix colnames FrenchSkiiers = Cold NoCold
	
	matrix list FrenchSkiiers
	
## Theme Music

<iframe width="560" height="315" src="https://www.youtube.com/embed/_om0W6xzQd8" frameborder="0" allow="accelerometer; autoplay; encrypted-media; gyroscope; picture-in-picture" allowfullscreen></iframe>

## Try Making a Data Set From Matrix
	
	svmat FrenchSkiiers, name(count)
	
	list
	
## Enter Data By Hand

There are many alternative commands to do this, but the easiest way is using `edit`.

I have already done this. Note the structure of the data is different from above.

    use "FrenchSkiiers.dta", clear
	
	list // list the data
	
## Mosaic Plot

	spineplot Tx Outcome, scheme(burd)
	
	graph export FrenchSkiiers1.png, width(500) replace
	
![Mosaic Plot Attempt 1](FrenchSkiiers1.png)

   spineplot Outcome Tx [fweight=Count], scheme(burd) // order matters to interpretability
   
   graph export FrenchSkiiers2.png, width(500) replace

![Mosaic Plot Attempt 2](FrenchSkiiers2.png)

# Definitions and Notation

## Counts

$\begin{matrix} c_{ij} & c_{ij} & c_{i\bullet} \\ c_{ij} & c_{ij} & c_{i\bullet} \\ c_{\bullet j} & c_{\bullet j} & c_{\bullet \bullet} \\ \end{matrix}$

## Probabilities

$\begin{matrix}p_{ij} & p_{ij} & p_{i\bullet} \\ p_{ij} & p_{ij} & p_{i \bullet} \\ p_{\bullet j} & p_{\bullet j} & p_{\bullet \bullet} \\ \end{matrix}$

## Terms

$p_{ij}$ are *joint* probabilities.

$p_{i \bullet}$ and $p_{\bullet j}$ are *marginal* probabilities.

$p_{ij} \mid p_{i \bullet}$ and $p_{ij} \mid p_{\bullet j}$ are *conditional* probabilities.





